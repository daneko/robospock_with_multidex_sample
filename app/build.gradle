buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:2.4.1'
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def j8Home = properties.getProperty('java8_home')
def j7Home = properties.getProperty('java7_home')

apply plugin: 'com.android.application'
apply plugin: 'retrolambda'

android {

    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.example.myapplication"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        debug {
            multiDexEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        javaMaxHeapSize '4g'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:recyclerview-v7:21.0.3'
    compile 'com.android.support:cardview-v7:21.0.3'
    // 6.5.87から細分化されているので、必要なものだけ入れれば良い
    compile 'com.google.android.gms:play-services:6.5.87'

    compile 'com.jakewharton:butterknife:6.0.0'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'org.functionaljava:functionaljava:4.2'
    compile 'com.fasterxml.jackson.core:jackson-core:2.5.0'

    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'joda-time:joda-time:2.6'

    compile 'com.github.tony19:logback-android-core:1.1.1-3'
    compile 'com.github.tony19:logback-android-classic:1.1.1-3'

    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:1.8.0'
    provided 'org.projectlombok:lombok:1.14.8'

    // multidexを試すためだけに追加
    compile 'com.google.guava:guava:18.0'

    debugCompile 'com.android.support:multidex:1.0.1'
}

retrolambda {
    jdk j8Home ?: System.getenv("JAVA8_HOME")
    oldJdk j7Home ?: System.getenv("JAVA7_HOME")
    javaVersion JavaVersion.VERSION_1_7
}

task zip2jar(type: Jar, dependsOn: ['compileDebugJava']) {
    from 'build/intermediates/classes/debug'
    destinationDir = file('build/libs')
}

